apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion 30
  buildToolsVersion "29.0.2"

  defaultConfig {
    minSdkVersion 19
    targetSdkVersion 30
    versionCode 1
    versionName "1.0"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
      all {
        // Enable forking to ensure each test is properly run in isolation. For context, see:
        // https://discuss.gradle.org/t/36066/2 & https://github.com/oppia/oppia-android/issues/1942
        forkEvery = 1
        maxParallelForks = Runtime.getRuntime().availableProcessors()

        // https://discuss.gradle.org/t/29495/2 & https://stackoverflow.com/a/34299238.
        testLogging {
          events "passed", "skipped", "failed"
          showExceptions = true
          exceptionFormat = "full"
          showCauses = true
          showStackTraces = true
          showStandardStreams = false
        }
      }
    }
  }
}

// See notice for the excluded files in domain/build.gradle for an explanation. Note that these
// tests can't be built in Gradle since they depend on app layer dependencies & Gradle doesn't allow
// a dependency between this & the app module since the latter is an Android application rather than
// a library.
def filesToExclude = [
    '**/*InitializeDefaultLocaleRuleCustomContextTest*.kt',
    '**/*InitializeDefaultLocaleRuleOmissionTest*.kt',
    '**/*InitializeDefaultLocaleRuleTest*.kt',
]
tasks.withType(SourceTask.class).configureEach {
  it.exclude(filesToExclude)
}
android.sourceSets.test.java.exclude(filesToExclude)
android.sourceSets.test.kotlin.exclude(filesToExclude)

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation(
      'androidx.annotation:annotation:1.1.0',
      'androidx.appcompat:appcompat:1.1.0',
      'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0-alpha03',
      'androidx.core:core-ktx:1.0.2',
      "androidx.test:core:1.0.0",
      'androidx.test.espresso:espresso-accessibility:3.1.0',
      'androidx.test.espresso:espresso-core:3.2.0',
      'androidx.test:runner:1.2.0',
      'com.google.android.material:material:1.3.0',
      'com.google.dagger:dagger:2.24',
      'com.google.protobuf:protobuf-javalite:3.17.3',
      'com.google.truth:truth:1.1.3',
      'com.google.truth.extensions:truth-liteproto-extension:1.1.3',
      'nl.dionsegijn:konfetti:1.2.5',
      'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.2.2',
      'org.robolectric:robolectric:4.5',
      'org.jetbrains.kotlin:kotlin-reflect:$kotlin_version',
      'org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version',
      'org.mockito:mockito-core:2.19.0',
      project(":domain"),
      project(":model"),
      project(":utility"),
  )
  compileOnly(
      'jakarta.xml.bind:jakarta.xml.bind-api:2.3.2',
      'javax.annotation:javax.annotation-api:1.3.2',
      'org.glassfish.jaxb:jaxb-runtime:2.3.2',
  )
  testImplementation(
      'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0-alpha03',
      'androidx.test.ext:junit:1.1.1',
      'com.google.android.material:material:1.3.0',
      'com.google.truth:truth:1.1.3',
      'junit:junit:4.12',
      "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version",
      'org.mockito:mockito-core:2.19.0',
      project(":domain"),
  )
  kapt(
      'com.google.dagger:dagger-compiler:2.24',
  )
  kaptTest(
      'com.google.dagger:dagger-compiler:2.24',
  )
  annotationProcessor(
      'com.google.auto.service:auto-service:1.0-rc4',
  )
  // TODO(#59): Remove this once Bazel is introduced
  // sufficiently visible for generated Dagger code. This can be done more cleanly via Bazel since dependencies can be
  // controlled more directly than in Gradle.
  implementation project(':model')
}
// The GeneratedMessageLite implementations of protobufs are depending on protobuf-java
// instead of protobuf-lite after Android Studio 3.5,
// The below command stops that from happening: https://github.com/google/tink/issues/282
configurations {
  all*.exclude module: 'protobuf-java'
}
