# TODO(#1532): Rename file to 'BUILD' post-Gradle.
"""
This library contains the app's core source files and functionality.
Note that:
 - Most files should be built inside of app library. However, listener files, annotation
files, view files, view model files, and binding adapter files need to be manually added. The
documentation below specifies where these files should be added.
- If your file uses Data-binding views or Kotlin synthetic imports these must be removed in
order to build with Bazel.
- All binding adapters must be written in Java.
"""

load("@dagger//:workspace_defs.bzl", "dagger_rules")
load("@io_bazel_rules_kotlin//kotlin:kotlin.bzl", "kt_android_library")
load("@tools_android//tools/crashlytics:defs.bzl", "crashlytics_android_library")
load("@tools_android//tools/googleservices:defs.bzl", "google_services_xml")
load("//app:app_test.bzl", "app_test")
load("//app:test_with_resources.bzl", "test_with_resources")

package(default_visibility = ["//utility:__subpackages__"])

exports_files(["src/main/AndroidManifest.xml"])

# Source files for the migrated source files library. The files inside the migrated source files
# library are dependencies in app module that have their own libraries.
# Place your files here if:
#  - A file of any type has their own BAZEL library
# NOTE: if the file is added here make sure to remove this from the other sub lists
# of EXCLUDED_APP_LIB_FILES.

# keep sorted
MIGRATED_SOURCE_FILES = [
    "src/main/java/org/oppia/android/app/viewmodel/ObservableArrayList.kt",
    "src/main/java/org/oppia/android/app/viewmodel/ObservableViewModel.kt",
]

# Source files for the annotations library. The files inside the annotations library are dependencies
# for all other libraries in app module, except the listeners library.
# Place your file here if:
#  - A view, view model, or layout file depends on it
#  - It is an annotation or helper file of some kind
#  - It is not considered a listener
#  - It does not depend on any other file not included in this list

# keep sorted
ANNOTATIONS = [
    "src/main/java/org/oppia/android/app/activity/ActivityScope.kt",
    "src/main/java/org/oppia/android/app/fragment/FragmentScope.kt",
    "src/main/java/org/oppia/android/app/utility/ContextExtensions.kt",
    "src/main/java/org/oppia/android/app/utility/KeyboardHelper.kt",
]

# Source files for the listeners library. The files inside the listeners library are dependencies for
# all other libraries in app module, except the annotations library.
# Place your file here if:
#  - A view, view model, or layout file depends on it
#  - It is either a listener, an interface, or a direct dependency of one in this list

# keep sorted
LISTENERS = [
    "src/main/java/org/oppia/android/app/administratorcontrols/LoadAppVersionListener.kt",
    "src/main/java/org/oppia/android/app/administratorcontrols/LoadProfileListListener.kt",
    "src/main/java/org/oppia/android/app/administratorcontrols/RouteToAppVersionListener.kt",
    "src/main/java/org/oppia/android/app/administratorcontrols/RouteToProfileListListener.kt",
    "src/main/java/org/oppia/android/app/drawer/RouteToProfileProgressListener.kt",
    "src/main/java/org/oppia/android/app/help/RouteToFAQListListener.kt",
    "src/main/java/org/oppia/android/app/help/faq/RouteToFAQSingleListener.kt",
    "src/main/java/org/oppia/android/app/home/RouteToRecentlyPlayedListener.kt",
    "src/main/java/org/oppia/android/app/home/RouteToTopicListener.kt",
    "src/main/java/org/oppia/android/app/home/RouteToTopicPlayStoryListener.kt",
    "src/main/java/org/oppia/android/app/home/recentlyplayed/OngoingStoryClickListener.kt",
    "src/main/java/org/oppia/android/app/home/topiclist/TopicSummaryClickListener.kt",
    "src/main/java/org/oppia/android/app/onboarding/OnboardingNavigationListener.kt",
    "src/main/java/org/oppia/android/app/onboarding/RouteToProfileListListener.kt",
    "src/main/java/org/oppia/android/app/options/LanguageRadioButtonListener.kt",
    "src/main/java/org/oppia/android/app/options/LoadAppLanguageListListener.kt",
    "src/main/java/org/oppia/android/app/options/LoadAudioLanguageListListener.kt",
    "src/main/java/org/oppia/android/app/options/LoadReadingTextSizeListener.kt",
    "src/main/java/org/oppia/android/app/options/RouteToAppLanguageListListener.kt",
    "src/main/java/org/oppia/android/app/options/RouteToAudioLanguageListListener.kt",
    "src/main/java/org/oppia/android/app/options/RouteToReadingTextSizeListener.kt",
    "src/main/java/org/oppia/android/app/player/audio/LanguageInterface.kt",
    "src/main/java/org/oppia/android/app/player/state/answerhandling/InteractionAnswerErrorOrAvailabilityCheckReceiver.kt",
    "src/main/java/org/oppia/android/app/player/state/answerhandling/InteractionAnswerHandler.kt",
    "src/main/java/org/oppia/android/app/player/state/listener/ContinueNavigationButtonListener.kt",
    "src/main/java/org/oppia/android/app/player/state/listener/NextNavigationButtonListener.kt",
    "src/main/java/org/oppia/android/app/player/state/listener/PreviousNavigationButtonListener.kt",
    "src/main/java/org/oppia/android/app/player/state/listener/PreviousResponsesHeaderClickListener.kt",
    "src/main/java/org/oppia/android/app/player/state/listener/ReplayButtonListener.kt",
    "src/main/java/org/oppia/android/app/player/state/listener/ReturnToTopicNavigationButtonListener.kt",
    "src/main/java/org/oppia/android/app/player/state/listener/RouteToHintsAndSolutionListener.kt",
    "src/main/java/org/oppia/android/app/player/state/listener/StateKeyboardButtonListener.kt",
    "src/main/java/org/oppia/android/app/player/state/listener/SubmitNavigationButtonListener.kt",
    "src/main/java/org/oppia/android/app/profile/RouteToAdminPinListener.kt",
    "src/main/java/org/oppia/android/app/profileprogress/ProfilePictureClickListener.kt",
    "src/main/java/org/oppia/android/app/profileprogress/RouteToCompletedStoryListListener.kt",
    "src/main/java/org/oppia/android/app/profileprogress/RouteToOngoingTopicListListener.kt",
    "src/main/java/org/oppia/android/app/recyclerview/OnDragEndedListener.kt",
    "src/main/java/org/oppia/android/app/recyclerview/OnItemDragListener.kt",
    "src/main/java/org/oppia/android/app/story/ExplorationSelectionListener.kt",
    "src/main/java/org/oppia/android/app/topic/RouteToRevisionCardListener.kt",
    "src/main/java/org/oppia/android/app/topic/lessons/ChapterSummarySelector.kt",
    "src/main/java/org/oppia/android/app/topic/lessons/StorySummarySelector.kt",
    "src/main/java/org/oppia/android/app/topic/revision/RevisionSubtopicSelector.kt",
    "src/main/java/org/oppia/android/app/topic/revisioncard/ReturnToTopicClickListener.kt",
    "src/main/java/org/oppia/android/app/utility/OnClickableAreaClickedListener.kt",
    "src/main/java/org/oppia/android/app/utility/RegionClickEvent.kt",
    "src/main/java/org/oppia/android/app/walkthrough/WalkthroughActivityListener.kt",
    "src/main/java/org/oppia/android/app/walkthrough/WalkthroughFragmentChangeListener.kt",
    "src/main/java/org/oppia/android/app/walkthrough/WalkthroughPageChanger.kt",
    "src/main/java/org/oppia/android/app/walkthrough/end/WalkthroughEndPageChanger.kt",
]

# Source files for the databinding_resources library.
# The files inside this list include all layout files. Please note that layouts MUST NOT depend on
# presenters or fragments. Instead, they should depend on a listener or interface as an abstraction.

# keep sorted
DATABINDING_LAYOUTS = ["src/main/res/layout*/**"]

# View Models

# TODO(#1617): Remove genrules post-gradle
# Source files for the view_models library that import resources.
# Place your file here if:
#  - It is a view model
#  - It imports resources (ex: `import org.oppia.android.R`)

# keep sorted
VIEW_MODELS_WITH_RESOURCE_IMPORTS = [
    "src/main/java/org/oppia/android/app/administratorcontrols/administratorcontrolsitemviewmodel/AdministratorControlsAccountActionsViewModel.kt",
    "src/main/java/org/oppia/android/app/help/HelpItemViewModel.kt",
    "src/main/java/org/oppia/android/app/help/HelpListViewModel.kt",
    "src/main/java/org/oppia/android/app/help/faq/FAQListViewModel.kt",
    "src/main/java/org/oppia/android/app/home/HomeViewModel.kt",
    "src/main/java/org/oppia/android/app/home/promotedlist/ComingSoonTopicsViewModel.kt",
    "src/main/java/org/oppia/android/app/home/promotedlist/PromotedStoryListViewModel.kt",
    "src/main/java/org/oppia/android/app/home/promotedlist/PromotedStoryViewModel.kt",
    "src/main/java/org/oppia/android/app/home/topiclist/TopicSummaryViewModel.kt",
    "src/main/java/org/oppia/android/app/onboarding/OnboadingSlideViewModel.kt",
    "src/main/java/org/oppia/android/app/onboarding/OnboardingViewModel.kt",
    "src/main/java/org/oppia/android/app/parser/StringToFractionParser.kt",
    "src/main/java/org/oppia/android/app/parser/StringToNumberParser.kt",
    "src/main/java/org/oppia/android/app/parser/StringToRatioParser.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/FractionInteractionViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/ImageRegionSelectionInteractionViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/RatioExpressionInputInteractionViewModel.kt",
    "src/main/java/org/oppia/android/app/profileprogress/ProfileProgressViewModel.kt",
    "src/main/java/org/oppia/android/app/topic/info/TopicInfoViewModel.kt",
    "src/main/java/org/oppia/android/app/utility/RatioExtensions.kt",
]

# Source files for the view_models library that DO NOT import resources.
# Place your file here if:
#  - It is a view model
#  - It does not import resources

# keep sorted
VIEW_MODELS = [
    "src/main/java/org/oppia/android/app/administratorcontrols/administratorcontrolsitemviewmodel/AdministratorControlsAppInformationViewModel.kt",
    "src/main/java/org/oppia/android/app/administratorcontrols/administratorcontrolsitemviewmodel/AdministratorControlsDownloadPermissionsViewModel.kt",
    "src/main/java/org/oppia/android/app/administratorcontrols/administratorcontrolsitemviewmodel/AdministratorControlsGeneralViewModel.kt",
    "src/main/java/org/oppia/android/app/administratorcontrols/administratorcontrolsitemviewmodel/AdministratorControlsItemViewModel.kt",
    "src/main/java/org/oppia/android/app/administratorcontrols/administratorcontrolsitemviewmodel/AdministratorControlsProfileViewModel.kt",
    "src/main/java/org/oppia/android/app/administratorcontrols/AdministratorControlsViewModel.kt",
    "src/main/java/org/oppia/android/app/administratorcontrols/appversion/AppVersionViewModel.kt",
    "src/main/java/org/oppia/android/app/completedstorylist/CompletedStoryItemViewModel.kt",
    "src/main/java/org/oppia/android/app/completedstorylist/CompletedStoryListViewModel.kt",
    "src/main/java/org/oppia/android/app/drawer/NavigationDrawerFooterViewModel.kt",
    "src/main/java/org/oppia/android/app/drawer/NavigationDrawerHeaderViewModel.kt",
    "src/main/java/org/oppia/android/app/help/faq/faqItemViewModel/FAQContentViewModel.kt",
    "src/main/java/org/oppia/android/app/help/faq/faqItemViewModel/FAQHeaderViewModel.kt",
    "src/main/java/org/oppia/android/app/help/faq/faqItemViewModel/FAQItemViewModel.kt",
    "src/main/java/org/oppia/android/app/help/HelpItems.kt",
    "src/main/java/org/oppia/android/app/hintsandsolution/HintsAndSolutionItemViewModel.kt",
    "src/main/java/org/oppia/android/app/hintsandsolution/HintsDividerViewModel.kt",
    "src/main/java/org/oppia/android/app/hintsandsolution/HintsViewModel.kt",
    "src/main/java/org/oppia/android/app/hintsandsolution/SolutionViewModel.kt",
    "src/main/java/org/oppia/android/app/home/HomeItemViewModel.kt",
    "src/main/java/org/oppia/android/app/home/promotedlist/ComingSoonTopicListViewModel.kt",
    "src/main/java/org/oppia/android/app/home/recentlyplayed/OngoingStoryViewModel.kt",
    "src/main/java/org/oppia/android/app/home/recentlyplayed/RecentlyPlayedItemViewModel.kt",
    "src/main/java/org/oppia/android/app/home/recentlyplayed/SectionTitleViewModel.kt",
    "src/main/java/org/oppia/android/app/home/topiclist/AllTopicsViewModel.kt",
    "src/main/java/org/oppia/android/app/home/UserAppHistoryViewModel.kt",
    "src/main/java/org/oppia/android/app/home/WelcomeViewModel.kt",
    "src/main/java/org/oppia/android/app/onboarding/OnboardingSlideFinalViewModel.kt",
    "src/main/java/org/oppia/android/app/onboarding/OnboardingViewPagerViewModel.kt",
    "src/main/java/org/oppia/android/app/onboarding/ViewPagerSlide.kt",
    "src/main/java/org/oppia/android/app/ongoingtopiclist/OngoingTopicItemViewModel.kt",
    "src/main/java/org/oppia/android/app/ongoingtopiclist/OngoingTopicListViewModel.kt",
    "src/main/java/org/oppia/android/app/options/LanguageItemViewModel.kt",
    "src/main/java/org/oppia/android/app/options/LanguageSelectionViewModel.kt",
    "src/main/java/org/oppia/android/app/options/OptionControlsViewModel.kt",
    "src/main/java/org/oppia/android/app/options/OptionsAppLanguageViewModel.kt",
    "src/main/java/org/oppia/android/app/options/OptionsAudioLanguageViewModel.kt",
    "src/main/java/org/oppia/android/app/options/OptionsItemViewModel.kt",
    "src/main/java/org/oppia/android/app/options/OptionsReadingTextSizeViewModel.kt",
    "src/main/java/org/oppia/android/app/player/audio/AudioViewModel.kt",
    "src/main/java/org/oppia/android/app/player/exploration/ExplorationViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/ContentViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/ContinueInteractionViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/ContinueNavigationButtonViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/DragAndDropSortInteractionViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/DragDropInteractionContentViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/FeedbackViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/NextButtonViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/NumericInputViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/PreviousButtonViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/PreviousResponsesHeaderViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/ReplayButtonViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/ReturnToTopicButtonViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/SelectionInteractionContentViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/SelectionInteractionViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/StateItemViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/SubmitButtonViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/SubmittedAnswerViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/itemviewmodel/TextInputViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/StateViewModel.kt",
    "src/main/java/org/oppia/android/app/player/state/testing/StateFragmentTestViewModel.kt",
    "src/main/java/org/oppia/android/app/profile/AddProfileViewModel.kt",
    "src/main/java/org/oppia/android/app/profile/AdminAuthViewModel.kt",
    "src/main/java/org/oppia/android/app/profile/AdminPinViewModel.kt",
    "src/main/java/org/oppia/android/app/profile/AdminSettingsViewModel.kt",
    "src/main/java/org/oppia/android/app/profile/PinPasswordViewModel.kt",
    "src/main/java/org/oppia/android/app/profile/ProfileChooserViewModel.kt",
    "src/main/java/org/oppia/android/app/profile/ResetPinViewModel.kt",
    "src/main/java/org/oppia/android/app/profileprogress/ProfilePictureActivityViewModel.kt",
    "src/main/java/org/oppia/android/app/profileprogress/ProfileProgressHeaderViewModel.kt",
    "src/main/java/org/oppia/android/app/profileprogress/ProfileProgressItemViewModel.kt",
    "src/main/java/org/oppia/android/app/profileprogress/RecentlyPlayedStorySummaryViewModel.kt",
    "src/main/java/org/oppia/android/app/recyclerview/BindableAdapter.kt",
    "src/main/java/org/oppia/android/app/recyclerview/DividerItemDecorator.kt",
    "src/main/java/org/oppia/android/app/recyclerview/DragAndDropItemFacilitator.kt",
    "src/main/java/org/oppia/android/app/settings/profile/ProfileEditViewModel.kt",
    "src/main/java/org/oppia/android/app/settings/profile/ProfileListViewModel.kt",
    "src/main/java/org/oppia/android/app/settings/profile/ProfileRenameViewModel.kt",
    "src/main/java/org/oppia/android/app/settings/profile/ProfileResetPinViewModel.kt",
    "src/main/java/org/oppia/android/app/shim/IntentFactoryShim.kt",
    "src/main/java/org/oppia/android/app/story/StoryFragmentScroller.kt",
    "src/main/java/org/oppia/android/app/story/storyitemviewmodel/StoryChapterSummaryViewModel.kt",
    "src/main/java/org/oppia/android/app/story/storyitemviewmodel/StoryHeaderViewModel.kt",
    "src/main/java/org/oppia/android/app/story/storyitemviewmodel/StoryItemViewModel.kt",
    "src/main/java/org/oppia/android/app/story/StoryViewModel.kt",
    "src/main/java/org/oppia/android/app/testing/BindableAdapterTestDataModel.kt",
    "src/main/java/org/oppia/android/app/testing/BindableAdapterTestViewModel.kt",
    "src/main/java/org/oppia/android/app/topic/conceptcard/ConceptCardViewModel.kt",
    "src/main/java/org/oppia/android/app/topic/lessons/StorySummaryViewModel.kt",
    "src/main/java/org/oppia/android/app/topic/lessons/ChapterSummaryViewModel.kt",
    "src/main/java/org/oppia/android/app/topic/lessons/TopicLessonsItemViewModel.kt",
    "src/main/java/org/oppia/android/app/topic/lessons/TopicLessonsTitleViewModel.kt",
    "src/main/java/org/oppia/android/app/topic/practice/practiceitemviewmodel/TopicPracticeFooterViewModel.kt",
    "src/main/java/org/oppia/android/app/topic/practice/practiceitemviewmodel/TopicPracticeHeaderViewModel.kt",
    "src/main/java/org/oppia/android/app/topic/practice/practiceitemviewmodel/TopicPracticeItemViewModel.kt",
    "src/main/java/org/oppia/android/app/topic/practice/practiceitemviewmodel/TopicPracticeSubtopicViewModel.kt",
    "src/main/java/org/oppia/android/app/topic/practice/TopicPracticeViewModel.kt",
    "src/main/java/org/oppia/android/app/topic/questionplayer/QuestionPlayerViewModel.kt",
    "src/main/java/org/oppia/android/app/topic/revision/revisionitemviewmodel/TopicRevisionItemViewModel.kt",
    "src/main/java/org/oppia/android/app/topic/revision/TopicRevisionViewModel.kt",
    "src/main/java/org/oppia/android/app/topic/revisioncard/RevisionCardViewModel.kt",
    "src/main/java/org/oppia/android/app/topic/TopicViewModel.kt",
    "src/main/java/org/oppia/android/app/walkthrough/end/WalkthroughFinalViewModel.kt",
    "src/main/java/org/oppia/android/app/walkthrough/topiclist/topiclistviewmodel/WalkthroughTopicHeaderViewModel.kt",
    "src/main/java/org/oppia/android/app/walkthrough/topiclist/topiclistviewmodel/WalkthroughTopicSummaryViewModel.kt",
    "src/main/java/org/oppia/android/app/walkthrough/topiclist/WalkthroughTopicItemViewModel.kt",
    "src/main/java/org/oppia/android/app/walkthrough/topiclist/WalkthroughTopicViewModel.kt",
    "src/main/java/org/oppia/android/app/walkthrough/WalkthroughViewModel.kt",
    "src/main/java/org/oppia/android/app/walkthrough/welcome/WalkthroughWelcomeViewModel.kt",
] + [
    "update_" + view_models_with_resource_imports[0:-3]
    for view_models_with_resource_imports in VIEW_MODELS_WITH_RESOURCE_IMPORTS
]

# TODO(#1617): Remove genrules post-gradle
# Genrule for source files in the view_models library.
# Because each databinding library must have a unique package name and manifest, resources must be
# imported using the proper package name when building with Bazel. This genrule alters those imports
# in order to keep Gradle building.

[
    genrule(
        name = "update_" + file[0:-3],
        srcs = [file],
        outs = [file[0:-3] + "_updated.kt"],
        cmd = """
    cat $(SRCS) |
    sed 's/import org.oppia.android.R/import org.oppia.android.app.view.models.R/g' > $(OUTS)
    """,
    )
    for file in VIEW_MODELS_WITH_RESOURCE_IMPORTS
]

# Views

# TODO(#1617): Remove genrules post-gradle
# Source files for the views library that import resources.
# Place your file here if:
#  - It is a view file
#  - It imports resources (ex: `import org.oppia.android.R`)

# keep sorted
VIEWS_WITH_RESOURCE_IMPORTS = [
    "src/main/java/org/oppia/android/app/customview/LessonThumbnailImageView.kt",
    "src/main/java/org/oppia/android/app/customview/SegmentedCircularProgressView.kt",
    "src/main/java/org/oppia/android/app/utility/ClickableAreasImage.kt",
]

# Source files for the views library that DO NOT import resources.
# Place your file here if:
#  - It is a view file
#  - It does not import resources
#  - It holds a circular dependency with a view (including views in VIEWS_WITH_RESOURCE_IMPORTS)

# keep sorted
VIEWS = [
    "src/main/java/org/oppia/android/app/application/ApplicationInjector.kt",
    "src/main/java/org/oppia/android/app/application/ApplicationInjectorProvider.kt",
    "src/main/java/org/oppia/android/app/customview/interaction/FractionInputInteractionView.kt",
    "src/main/java/org/oppia/android/app/customview/interaction/NumericInputInteractionView.kt",
    "src/main/java/org/oppia/android/app/customview/interaction/TextInputInteractionView.kt",
    "src/main/java/org/oppia/android/app/customview/interaction/RatioInputInteractionView.kt",
    "src/main/java/org/oppia/android/app/home/promotedlist/ComingSoonTopicsListView.kt",
    "src/main/java/org/oppia/android/app/home/promotedlist/PromotedStoryListView.kt",
    "src/main/java/org/oppia/android/app/player/state/DragDropSortInteractionView.kt",
    "src/main/java/org/oppia/android/app/player/state/ImageRegionSelectionInteractionView.kt",
    "src/main/java/org/oppia/android/app/player/state/SelectionInteractionView.kt",
    "src/main/java/org/oppia/android/app/shim/ViewBindingShim.kt",
    "src/main/java/org/oppia/android/app/shim/ViewComponentFactory.kt",
    "src/main/java/org/oppia/android/app/view/ViewComponent.kt",
    "src/main/java/org/oppia/android/app/view/ViewScope.kt",
] + [
    "update_" + views_with_resource_imports[0:-3]
    for views_with_resource_imports in VIEWS_WITH_RESOURCE_IMPORTS
]

# TODO(#1617): Remove genrules post-gradle
# Genrule for source files in the views library.
# Because each databinding library must have a unique package name and manifest, resources must be
# imported using the proper package name when building with Bazel. This genrule alters those imports
# in order to keep Gradle building.

[
    genrule(
        name = "update_" + file[0:-3],
        srcs = [file],
        outs = [file[0:-3] + "_updated.kt"],
        cmd = """
     cat $(SRCS) |
     sed 's/import org.oppia.android.R/import org.oppia.android.app.views.R/g' > $(OUTS)
     """,
    )
    for file in VIEWS_WITH_RESOURCE_IMPORTS
]

# Binding Adapters

# TODO(#1617): Remove genrules post-gradle
# Binding adapter files that import resources.
# Place your file here if:
#  - It is a binding adapter
#  - It imports resources (ex: `import org.oppia.android.R`)

# keep sorted
BINDING_ADAPTERS_WITH_RESOURCE_IMPORTS = [
    "src/main/java/org/oppia/android/app/databinding/DrawableBindingAdapters.java",
    "src/main/java/org/oppia/android/app/databinding/ImageViewBindingAdapters.java",
    "src/main/java/org/oppia/android/app/databinding/TextViewBindingAdapters.java",
]

# Binding adapter files that DO NOT import resources.
# Place your file here if:
#  - It is a binding adapter
#  - It does not import resources

# keep sorted
BINDING_ADAPTERS = [
    "src/main/java/org/oppia/android/app/databinding/ConstraintLayoutAdapters.java",
    "src/main/java/org/oppia/android/app/databinding/EditTextBindingAdapters.java",
    "src/main/java/org/oppia/android/app/databinding/GuidelineBindingAdapters.java",
    "src/main/java/org/oppia/android/app/databinding/MarginBindingAdapters.java",
    "src/main/java/org/oppia/android/app/databinding/PinViewBindingAdapters.java",
    "src/main/java/org/oppia/android/app/databinding/StateAssemblerMarginBindingAdapters.java",
    "src/main/java/org/oppia/android/app/databinding/StateAssemblerPaddingBindingAdapters.java",
    "src/main/java/org/oppia/android/app/databinding/TextInputLayoutBindingAdapters.java",
    "src/main/java/org/oppia/android/app/databinding/ViewBindingAdapters.java",
    "src/main/java/org/oppia/android/app/recyclerview/RecyclerViewBindingAdapter.java",
] + [
    "update_" + binding_adapters_with_resource_imports[0:-5]
    for binding_adapters_with_resource_imports in BINDING_ADAPTERS_WITH_RESOURCE_IMPORTS
]

# TODO(#1617): Remove genrules post-gradle
# Genrule for source files in the binding_adapters library.
# Because each databinding library must have a unique package name and manifest, resources must be
# imported using the proper package name when building with Bazel. This genrule alters those imports
# in order to keep Gradle building.

[genrule(
    name = "update_" + file[0:-5],
    srcs = [file],
    outs = [file[0:-5] + "_updated.java"],
    cmd = """
     cat $(SRCS) |
     sed 's/import org.oppia.android.R/import org.oppia.android.app.databinding.adapters.R/g' |
     sed 's/BindingAdapters {/BindingAdapters_updated {/g' > $(OUTS)
     """,
) for file in BINDING_ADAPTERS_WITH_RESOURCE_IMPORTS]

# keep sorted
SNAP_HELPER = ["src/main/java/org/oppia/android/app/recyclerview/StartSnapHelper.kt"]

# Files to be excluded from the source list for the app library. This list includes all the files
# built by the following libraries:
# - view_models
# - views
# - listeners
# - annotations
# - binding_adapters

EXCLUDED_APP_LIB_FILES = VIEW_MODELS + VIEW_MODELS_WITH_RESOURCE_IMPORTS + VIEWS + VIEWS_WITH_RESOURCE_IMPORTS + BINDING_ADAPTERS + BINDING_ADAPTERS_WITH_RESOURCE_IMPORTS + LISTENERS + ANNOTATIONS + SNAP_HELPER + MIGRATED_SOURCE_FILES

# Files to be built by the app library. All of these files are assumed to have resource imports and
# will be processed by the app library genrule regardless in order to avoid any import issues.

APP_FILES_WITH_RESOURCE_IMPORTS = glob(
    ["src/main/java/org/oppia/android/app/**/*.kt"],
    exclude = EXCLUDED_APP_LIB_FILES,
)

# Genrule for source files in the app library.
# Because each databinding library must have a unique package name and manifest, resources must be
# imported using the proper package name when building with Bazel. This genrule alters those imports
# in order to keep Gradle building.

[
    genrule(
        name = "update_" + file[0:-3],
        srcs = [file],
        outs = [file[0:-3] + "_updated.kt"],
        cmd = """
     cat $(SRCS) |
     sed 's/import org.oppia.android.R/import org.oppia.android.app.ui.R/g' |
     sed 's/import org.oppia.android.databinding./import org.oppia.android.app.databinding.databinding./g' > $(OUTS)
     """,
    )
    for file in APP_FILES_WITH_RESOURCE_IMPORTS
]

# Files to be built by the app library.
APP_FILES = [] + [
    "update_" + app_files_with_resource_imports[0:-3]
    for app_files_with_resource_imports in APP_FILES_WITH_RESOURCE_IMPORTS
]

# Library for non-layout resource files.
android_library(
    name = "resources",
    custom_package = "org.oppia.android.app",
    exports_manifest = True,
    manifest = "src/main/AndroidManifest.xml",
    resource_files = glob(
        ["src/main/res/**"],
        exclude = DATABINDING_LAYOUTS,
    ),
    visibility = ["//visibility:private"],
    deps = ["//third_party:com_google_android_material_material"],
)

# Library for layout resource files.
android_library(
    name = "databinding_resources",
    srcs = BINDING_ADAPTERS,
    custom_package = "org.oppia.android.app.databinding",
    enable_data_binding = True,
    exports_manifest = True,
    manifest = "src/main/DatabindingResourcesManifest.xml",
    resource_files = glob(DATABINDING_LAYOUTS),
    deps = [
        ":annotations",
        ":binding_adapters",
        ":resources",
        ":view_models",
        ":views",
        "//model",
        "//third_party:androidx_annotation_annotation",
        "//third_party:androidx_constraintlayout_constraintlayout",
        "//third_party:androidx_core_core",
        "//third_party:androidx_databinding_databinding-adapters",
        "//third_party:androidx_databinding_databinding-common",
        "//third_party:androidx_databinding_databinding-runtime",
        "//third_party:androidx_drawerlayout_drawerlayout",
        "//third_party:androidx_lifecycle_lifecycle-livedata-core",
        "//third_party:androidx_recyclerview_recyclerview",
        "//third_party:androidx_viewpager2_viewpager2",
        "//third_party:androidx_viewpager_viewpager",
        "//third_party:circularimageview_circular_image_view",
        "//third_party:com_chaos_view_pinview",
        "//third_party:com_google_android_flexbox",
        "//third_party:com_google_android_material_material",
        "//third_party:de_hdodenhof_circleimageview",
        "//third_party:javax_annotation_javax_annotation-api_jar",
        "//third_party:nl_dionsegijn_konfetti",
    ],
)

# Library for listener files required to build views and view_model libraries.
kt_android_library(
    name = "listeners",
    srcs = LISTENERS,
    custom_package = "org.oppia.android.app",
    deps = [
        ":dagger",
        "//model",
        "//third_party:androidx_recyclerview_recyclerview",
    ],
)

# Library for all view files required to build layout files.
kt_android_library(
    name = "views",
    srcs = VIEWS,
    custom_package = "org.oppia.android.app.views",
    manifest = "src/main/ViewsManifest.xml",
    deps = [
        ":annotations",
        ":listeners",
        ":resources",
        ":snap_helper",
        ":view_models",
        "//model",
        "//third_party:androidx_appcompat_appcompat",
        "//third_party:androidx_core_core-ktx",
        "//third_party:androidx_databinding_databinding-common",
        "//third_party:androidx_databinding_databinding-runtime",
        "//third_party:circularimageview_circular_image_view",
    ],
)

# Library for scope annotations required to build views and view_model libraries.
kt_android_library(
    name = "annotations",
    srcs = ANNOTATIONS,
    custom_package = "org.oppia.android.app",
    deps = [
        ":dagger",
        "//model",
    ],
)

# Library for all view model files.
kt_android_library(
    name = "view_models",
    srcs = VIEW_MODELS,
    custom_package = "org.oppia.android.app.view.models",
    enable_data_binding = True,
    manifest = "src/main/ViewModelsManifest.xml",
    deps = [
        ":annotations",
        ":dagger",
        ":listeners",
        ":resources",
        "//app/src/main/java/org/oppia/android/app/viewmodel:observable_array_list",
        "//app/src/main/java/org/oppia/android/app/viewmodel:observable_view_model",
        "//app/src/main/java/org/oppia/android/app/viewmodel:view_model_provider",
        "//domain",
        "//model",
        "//third_party:androidx_databinding_databinding-common",
        "//third_party:androidx_databinding_databinding-runtime",
        "//utility",
    ],
)

# Library for snap helper used to build views.
kt_android_library(
    name = "snap_helper",
    srcs = SNAP_HELPER,
    custom_package = "org.oppia.android.app.recyclerview",
    deps = ["//third_party:androidx_recyclerview_recyclerview"],
)

# TODO(#1639): Translate binding adapters back to Kotlin post-Bazel modularization.
# Library for all custom data-binding adapters under org/oppia/android/app/databinding.
android_library(
    name = "binding_adapters",
    srcs = BINDING_ADAPTERS,
    custom_package = "org.oppia.android.app.databinding.adapters",
    enable_data_binding = True,
    manifest = "src/main/DatabindingAdaptersManifest.xml",
    deps = [
        ":dagger",
        ":resources",
        ":view_models",
        "//model",
        "//third_party:androidx_annotation_annotation",
        "//third_party:androidx_constraintlayout_constraintlayout",
        "//third_party:androidx_lifecycle_lifecycle-livedata-core",
        "//third_party:androidx_lifecycle_lifecycle-livedata-ktx",
        "//third_party:circularimageview_circular_image_view",
        "//third_party:com_chaos_view_pinview",
        "//third_party:com_google_android_material_material",
        "//utility",
    ],
)

# Library containing all activity, fragment, and view-based UI flows in the app.
kt_android_library(
    name = "app",
    srcs = APP_FILES,
    custom_package = "org.oppia.android.app.ui",
    enable_data_binding = 1,
    manifest = "src/main/AppAndroidManifest.xml",
    visibility = ["//visibility:public"],
    deps = [
        ":binding_adapters",
        ":dagger",
        ":databinding_resources",
        ":resources",
        ":view_models",
        ":views",
        "//model",
        "//third_party:androidx_databinding_databinding-adapters",
        "//third_party:androidx_databinding_databinding-common",
        "//third_party:androidx_databinding_databinding-runtime",
        "//third_party:androidx_lifecycle_lifecycle-extensions",
        "//third_party:androidx_lifecycle_lifecycle-livedata-core",
        "//third_party:androidx_lifecycle_lifecycle-livedata-ktx",
        "//third_party:androidx_multidex_multidex",
        "//third_party:androidx_viewpager2_viewpager2",
        "//third_party:androidx_viewpager_viewpager",
        "//third_party:androidx_work_work-runtime-ktx",
        "//third_party:com_caverock_androidsvg-aar",
        "//third_party:com_google_android_flexbox",
        "//third_party:javax_annotation_javax_annotation-api_jar",
        "//utility",
    ],
)

# Library that builds test-only source files.

# All app-specific files that are only used for testing
# purposes but are not tests should be included in this list. These files should not be included in
# the app library or the final binary but should be dependencies for tests.

# Place your file here if:
#  - It is not a test file
#  - Only test files depend on it

kt_android_library(
    name = "test_deps",
    testonly = True,
    srcs = [
        "src/sharedTest/java/org/oppia/android/app/parser/RichTextViewMatcher.kt",
        "src/sharedTest/java/org/oppia/android/app/recyclerview/RecyclerViewMatcher.kt",
        "src/sharedTest/java/org/oppia/android/app/utility/ClickActions.kt",
        "src/sharedTest/java/org/oppia/android/app/utility/DragViewAction.kt",
        "src/sharedTest/java/org/oppia/android/app/utility/DrawableMatcher.kt",
        "src/sharedTest/java/org/oppia/android/app/utility/EspressoTestsMatchers.kt",
        "src/sharedTest/java/org/oppia/android/app/utility/FontSizeMatcher.kt",
        "src/sharedTest/java/org/oppia/android/app/utility/MockitoKotlinHelper.kt",
        "src/sharedTest/java/org/oppia/android/app/utility/OrientationChangeAction.kt",
        "src/sharedTest/java/org/oppia/android/app/utility/ProgressMatcher.kt",
        "src/sharedTest/java/org/oppia/android/app/utility/TabMatcher.kt",
    ],
    deps = [
        ":app",
        "//testing",
        "//third_party:org_mockito_mockito-core",
    ],
)

# keep sorted
TEST_DEPS = [
    ":app",
    ":dagger",
    ":resources",
    ":test_deps",
    "//domain",
    "//testing",
    "//third_party:androidx_annotation_annotation",
    "//third_party:androidx_databinding_databinding-adapters",
    "//third_party:androidx_databinding_databinding-common",
    "//third_party:androidx_databinding_databinding-compiler",
    "//third_party:androidx_databinding_databinding-runtime",
    "//third_party:androidx_navigation_navigation-fragment",
    "//third_party:androidx_navigation_navigation-ui",
    "//third_party:androidx_test_espresso_espresso-contrib",
    "//third_party:androidx_test_espresso_espresso-core",
    "//third_party:androidx_test_espresso_espresso-intents",
    "//third_party:androidx_test_ext_junit",
    "//third_party:androidx_test_runner",
    "//third_party:com_github_bumptech_glide_mocks",
    "//third_party:com_google_truth_truth",
    "//third_party:org_jetbrains_kotlin_kotlin-reflect",
    "//third_party:org_jetbrains_kotlin_kotlin-test-junit",
    "//third_party:org_jetbrains_kotlinx_kotlinx-coroutines-test",
    "//third_party:org_mockito_mockito-core",
    "//third_party:org_robolectric_annotations",
    "//third_party:robolectric_android-all",
    "//utility",
]

# App module tests. Note that all tests are assumed to be tests with resources (even though not all
# are). Note that if a test file imports resources or binding files, it must be wrapped by
# test_with_resources().

# Qualified file paths for test classes that have been migrated over to their own packages &
# shouldn't be defined as module-level tests.
# keep sorted
MIGRATED_TESTS = [
]

# Unit tests.
[app_test(
    name = test_file_path,
    filtered_tests = MIGRATED_TESTS,
    manifest_values = {
        "applicationId": "org.oppia.android",
        "minSdkVersion": "19",
        "targetSdkVersion": "28",
        "versionCode": "0",
        "versionName": "0.1-alpha",
    },
    processed_src = test_with_resources(test_file_path),
    test_path_prefix = "src/test/java/",
    deps = TEST_DEPS,
) for test_file_path in glob(["src/test/java/org/oppia/android/app/**/*Test.kt"])]

# Shared tests.
[app_test(
    name = test_file_path,
    filtered_tests = MIGRATED_TESTS,
    manifest_values = {
        "applicationId": "org.oppia.android",
        "minSdkVersion": "19",
        "targetSdkVersion": "28",
        "versionCode": "0",
        "versionName": "0.1-alpha",
    },
    processed_src = test_with_resources(test_file_path),
    test_path_prefix = "src/sharedTest/java/",
    deps = TEST_DEPS,
) for test_file_path in glob(["src/sharedTest/java/org/oppia/android/app/**/*Test.kt"])]

# TODO(#1566): Move Firebase rules to their own package & remove default visibility
# Package for all Firebase dependencies.
# To reference these dependencies, add '//app:crashlytics' and '//app:crashlytics_deps'
# to your build rule's dependency list.

GOOGLE_SERVICES_RESOURCES = google_services_xml(
    package_name = "org.oppia.android",
    google_services_json = "google-services.json",
)

crashlytics_android_library(
    name = "crashlytics",
    package_name = "org.oppia.android",
    build_id = "48fc9d17-e102-444c-8e0d-638d75ec0942",
    resource_files = GOOGLE_SERVICES_RESOURCES,
)

android_library(
    name = "crashlytics_deps",
    exports = [
        "//third_party:com_crashlytics_sdk_android_crashlytics",
        "//third_party:com_google_firebase_firebase-analytics",
        "//third_party:com_google_firebase_firebase-crashlytics",
        "//third_party:io_fabric_sdk_android_fabric",
    ],
)

dagger_rules()
