"""
Configurations for a codebase-wide build toolchain for Kotlin.
"""

load("@bazel_skylib//rules:common_settings.bzl", "string_flag")
load("@io_bazel_rules_kotlin//kotlin:core.bzl", "define_kt_toolchain", "kt_javac_options", "kt_kotlinc_options")

# This exposes a patch file that retroactively adds opt-in support to kt_kotlinc_options (which was
# added in a later version of rules_kotlin that's not available for the version of Bazel used at the
# time of this introduction).
exports_files(["add_kotlinc_optin_support.patch"])

string_flag(
    name = "warn_mode",
    values = ["warning", "failure"],
    build_setting_default = "failure",
)

config_setting(
    name = "errors_as_warnings",
    flag_values = {":warn_mode": "warning"},
)

config_setting(
    name = "errors_as_failures",
    flag_values = {":warn_mode": "failure"},
)

kt_kotlinc_options(
    name = "oppia_kotlinc_options",
    warn = select({
        ":errors_as_warnings": "report",
        ":errors_as_failures": "error",
    }),
    x_optin = ["kotlin.RequiresOptIn"],
    x_use_experimental = False,
)

# TODO(#4936): Fix "warnings as errors" for processor-generated Java code.
kt_javac_options(
    name = "oppia_javac_options",
    warn = select({
        ":errors_as_warnings": "report",
        ":errors_as_failures": "error",
    }),
)

define_kt_toolchain(
    name = "kotlin_16_jdk9_toolchain",
    api_version = "1.6",
    javac_options = ":oppia_javac_options",
    jvm_target = "9",
    kotlinc_options = ":oppia_kotlinc_options",
    language_version = "1.6",
)
