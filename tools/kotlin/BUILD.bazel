"""
Configurations for a codebase-wide build toolchain for Kotlin.
"""

load("@bazel_skylib//rules:common_settings.bzl", "string_flag")
load("@io_bazel_rules_kotlin//kotlin:core.bzl", "define_kt_toolchain", "kt_javac_options", "kt_kotlinc_options")

# This exposes a patch that fixes an issue with duplicate processor arguments. See:
# github.com/bazelbuild/rules_kotlin/pull/940.
exports_files(["remove_processor_duplicates.patch"])

string_flag(
    name = "warn_mode",
    build_setting_default = "failure",
    values = [
        "warning",
        "failure",
    ],
)

config_setting(
    name = "errors_as_warnings",
    flag_values = {":warn_mode": "warning"},
)

config_setting(
    name = "errors_as_failures",
    flag_values = {":warn_mode": "failure"},
)

kt_kotlinc_options(
    name = "oppia_kotlinc_options",
    warn = select({
        ":errors_as_failures": "error",
        ":errors_as_warnings": "report",
    }),
    x_optin = ["kotlin.RequiresOptIn"],
)

# TODO(#4936): Fix "warnings as errors" for processor-generated Java code.
kt_javac_options(
    name = "oppia_javac_options",
    warn = select({
        ":errors_as_failures": "error",
        ":errors_as_warnings": "report",
    }),
)

define_kt_toolchain(
    name = "kotlin_16_jdk9_toolchain",
    api_version = "1.6",
    javac_options = ":oppia_javac_options",
    jvm_target = "9",
    kotlinc_options = ":oppia_kotlinc_options",
    language_version = "1.6",
)
