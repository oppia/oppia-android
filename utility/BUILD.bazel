# TODO(#1532): Rename file to 'BUILD' post-Gradle.
"""
This library contains utilities that all other modules, minus model, depend on.
"""

load("@dagger//:workspace_defs.bzl", "dagger_rules")
load("@io_bazel_rules_kotlin//kotlin:kotlin.bzl", "kt_android_library")
load("//utility:utility_test.bzl", "utility_test")

# Source files that have been migrated to their own package-based libraries. Files added to this
# list will automatically be excluded from the top-level utility library. It's recommended to use
# globs here to ensure new files added to migrated packages don't accidentally get included in the
# top-level module library.
MIGRATED_PROD_FILES = glob([
    "src/main/java/org/oppia/android/util/accessibility/*.kt",
    "src/main/java/org/oppia/android/util/caching/*.kt",
    "src/main/java/org/oppia/android/util/data/*.kt",
    "src/main/java/org/oppia/android/util/datetime/*.kt",
    "src/main/java/org/oppia/android/util/extensions/*.kt",
    "src/main/java/org/oppia/android/util/gcsresource/*.kt",
    "src/main/java/org/oppia/android/util/logging/*.kt",
    "src/main/java/org/oppia/android/util/math/**/*.kt",
    "src/main/java/org/oppia/android/util/networking/*.kt",
    "src/main/java/org/oppia/android/util/profile/*.kt",
    "src/main/java/org/oppia/android/util/statusbar/*.kt",
    "src/main/java/org/oppia/android/util/system/*.kt",
    "src/main/java/org/oppia/android/util/threading/*.kt",
])

android_library(
    name = "resources",
    custom_package = "org.oppia.android.util",
    manifest = "src/main/AndroidManifest.xml",
    resource_files = glob(["src/main/res/**/*.xml"]),
)

# Visibility for migrated utility tests.
package_group(
    name = "utility_testing_visibility",
    packages = [
        "//utility/src/test/...",
    ],
)

android_library(
    name = "test_resources",
    testonly = True,
    custom_package = "org.oppia.android.util",
    manifest = ":test_manifest",
    resource_files = glob(["src/test/res/**"]),
    visibility = [
        ":utility_testing_visibility",
        "//app/src/test/java/org/oppia/android/app/translation:__pkg__",
    ],
)

# Library for general-purpose utilities.
kt_android_library(
    name = "utility",
    srcs = glob(
        ["src/main/java/org/oppia/android/util/**/*.kt"],
        exclude = MIGRATED_PROD_FILES,
    ),
    visibility = ["//visibility:public"],
    deps = [
        ":dagger",
        ":resources",
        "//app:crashlytics",
        "//app:crashlytics_deps",
        "//model/src/main/proto:event_logger_java_proto_lite",
        "//model/src/main/proto:platform_parameter_java_proto_lite",
        "//third_party:androidx_appcompat_appcompat",
        "//third_party:androidx_room_room-runtime",
        "//third_party:androidx_work_work-runtime",
        "//third_party:androidx_work_work-runtime-ktx",
        "//third_party:com_caverock_androidsvg",
        "//third_party:com_github_bumptech_glide_glide",
        "//third_party:com_google_guava_guava",
        "//third_party:glide_compiler",
        "//third_party:org_jetbrains_kotlinx_kotlinx-coroutines-core",
        "//utility/src/main/java/org/oppia/android/util/caching:annotations",
        "//utility/src/main/java/org/oppia/android/util/caching:asset_repository",
        "//utility/src/main/java/org/oppia/android/util/gcsresource:annotations",
        "//utility/src/main/java/org/oppia/android/util/gcsresource:prod_module",
        "//utility/src/main/java/org/oppia/android/util/locale:oppia_locale",
        "//utility/src/main/java/org/oppia/android/util/logging:console_logger",
        "//utility/src/main/java/org/oppia/android/util/logging:exception_logger",
        "//utility/src/main/java/org/oppia/android/util/logging:prod_module",
        "//utility/src/main/java/org/oppia/android/util/system:oppia_clock",
        "//utility/src/main/java/org/oppia/android/util/system:prod_module",
        "//utility/src/main/java/org/oppia/android/util/threading:prod_module",
    ],
)

filegroup(
    name = "test_manifest",
    srcs = ["src/test/AndroidManifest.xml"],
    visibility = ["//:oppia_testing_visibility"],
)

# keep sorted
TEST_DEPS = [
    ":dagger",
    ":utility",
    "//app:crashlytics",
    "//app:crashlytics_deps",
    "//model/src/main/proto:test_models",
    "//testing",
    "//testing/src/main/java/org/oppia/android/testing/data:data_provider_test_monitor",
    "//testing/src/main/java/org/oppia/android/testing/mockito",
    "//testing/src/main/java/org/oppia/android/testing/robolectric:test_module",
    "//testing/src/main/java/org/oppia/android/testing/threading:test_module",
    "//testing/src/main/java/org/oppia/android/testing/time:test_module",
    "//third_party:androidx_test_ext_junit",
    "//third_party:androidx_test_ext_truth",
    "//third_party:com_google_truth_extensions_truth-liteproto-extension",
    "//third_party:com_google_truth_truth",
    "//third_party:org_jetbrains_kotlin_kotlin-reflect",
    "//third_party:org_jetbrains_kotlin_kotlin-test-junit",
    "//third_party:org_mockito_mockito-core",
    "//third_party:robolectric_android-all",
    "//utility/src/main/java/org/oppia/android/util/caching:asset_prod_module",
    "//utility/src/main/java/org/oppia/android/util/caching/testing:caching_test_module",
    "//utility/src/main/java/org/oppia/android/util/extensions:bundle_extensions",
    "//utility/src/main/java/org/oppia/android/util/locale:prod_module",
    "//utility/src/main/java/org/oppia/android/util/logging:event_bundle_creator",
    "//utility/src/main/java/org/oppia/android/util/networking:debug_module",
    "//utility/src/main/java/org/oppia/android/util/parser/html:tag_handlers",
    "//utility/src/main/java/org/oppia/android/util/parser/image:glide_image_loader",
    "//utility/src/main/java/org/oppia/android/util/parser/image:url_image_parser",
]

# Qualified file paths for test classes that have been migrated over to their own packages &
# shouldn't be defined as module-level tests.
# keep sorted
MIGRATED_TESTS = [
    "src/test/java/org/oppia/android/util/networking/NetworkConnectionUtilDebugImplTest.kt",
    "src/test/java/org/oppia/android/util/networking/NetworkConnectionUtilDebugModuleTest.kt",
    "src/test/java/org/oppia/android/util/networking/NetworkConnectionUtilProdImplTest.kt",
    "src/test/java/org/oppia/android/util/networking/NetworkConnectionUtilProdModuleTest.kt",
]

[utility_test(
    name = test_file_path,
    filtered_tests = MIGRATED_TESTS,
    deps = TEST_DEPS,
) for test_file_path in glob(["src/test/java/org/oppia/android/util/**/*Test.kt"])]

dagger_rules()
