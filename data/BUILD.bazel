# TODO(#1532): Rename file to 'BUILD' post-Gradle.
"""
This library provides data to the rest of the application.
"""

load("//defs:oppia_android_library.bzl", "oppia_android_assets_library", "oppia_android_library")
load("//defs:oppia_android_test.bzl", "oppia_android_test")

# Visibility for data tests.
package_group(
    name = "data_testing_visibility",
    packages = [
        "//data",
        "//data/src/test/...",
    ],
)

oppia_android_assets_library(
    name = "assets",
    testonly = True,
    assets = glob(["src/test/assets/**"]),
    assets_dir = "src/test/assets/",
)

# TODO(#1603): Remove this shared library once everything is modularized.
oppia_android_library(
    name = "_combined_test_lib_do_not_depend",
    testonly = True,
    srcs = glob(["src/test/java/org/oppia/android/data/**/*Test.kt"]),
    android_merge_deps = [":assets"],
    deps = [
        "//:dagger",
        "//data/src/main/java/org/oppia/android/data/backends/gae:network_config_annotations",
        "//data/src/main/java/org/oppia/android/data/backends/gae:network_config_prod_module",
        "//data/src/main/java/org/oppia/android/data/backends/gae:network_interceptors",
        "//data/src/main/java/org/oppia/android/data/backends/gae:oppia_retrofit",
        "//data/src/main/java/org/oppia/android/data/backends/gae:prod_module",
        "//data/src/main/java/org/oppia/android/data/backends/gae/api",
        "//data/src/main/java/org/oppia/android/data/backends/gae/model",
        "//testing",
        "//testing/src/main/java/org/oppia/android/testing/network",
        "//testing/src/main/java/org/oppia/android/testing/network:test_module",
        "//testing/src/main/java/org/oppia/android/testing/platformparameter:test_constants",
        "//testing/src/main/java/org/oppia/android/testing/robolectric:test_module",
        "//testing/src/main/java/org/oppia/android/testing/threading:annotations",
        "//testing/src/main/java/org/oppia/android/testing/threading:test_coroutine_dispatcher",
        "//testing/src/main/java/org/oppia/android/testing/threading:test_module",
        "//third_party:androidx_test_core",
        "//third_party:androidx_test_ext_junit",
        "//third_party:com_google_guava_guava",
        "//third_party:com_google_truth_truth",
        "//third_party:com_squareup_moshi_moshi",
        "//third_party:com_squareup_okhttp3_mockwebserver",
        "//third_party:com_squareup_okhttp3_okhttp",
        "//third_party:com_squareup_retrofit2_converter-moshi",
        "//third_party:com_squareup_retrofit2_retrofit",
        "//third_party:com_squareup_retrofit2_retrofit-mock",
        "//third_party:javax_inject_javax_inject",
        "//third_party:junit_junit",
        "//third_party:kotlinx-coroutines-core-jvm",
        "//third_party:org_robolectric_annotations",
        "//third_party:org_robolectric_shadows-framework",
        "//utility/src/main/java/org/oppia/android/util/data:async_data_subscription_manager",
        "//utility/src/main/java/org/oppia/android/util/data:data_providers",
    ],
)

[oppia_android_test(
    name = test_file_path[:test_file_path.find(".kt")] if "/" in test_file_path else test_file_path,
    srcs = [],
    temp_test_class_migration = test_file_path[test_file_path.find("src/test/java/") + len("src/test/java/"):test_file_path.find(".kt")].replace("/", "."),
    deps = [":_combined_test_lib_do_not_depend"],
) for test_file_path in glob(["src/test/java/org/oppia/android/data/**/*Test.kt"])]
