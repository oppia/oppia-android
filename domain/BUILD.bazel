# TODO(#1532): Rename file to 'BUILD' post-Gradle.
"""
This library contains the frontend controller and business service logic for the application.
"""

load("@io_bazel_rules_kotlin//kotlin:android.bzl", "kt_android_library")
load("//domain:domain_assets.bzl", "generate_assets_list_from_text_protos")
load("//domain:domain_test.bzl", "domain_test")

filegroup(
    name = "test_manifest",
    srcs = ["src/test/AndroidManifest.xml"],
    visibility = ["//:oppia_testing_visibility"],
)

# Visibility for migrated domain tests.
package_group(
    name = "domain_testing_visibility",
    packages = [
        "//domain/src/test/...",
    ],
)

# Source files that have been migrated to their own package-based libraries. Files added to this
# list will be excluded automatically from the top-level utility library. It is recommended to use
# globs here to ensure that new files added to migrated packages don't accidentally get included in the
# top-level module library.
MIGRATED_PROD_FILES = glob([
    "src/main/java/org/oppia/android/domain/auth/*.kt",
    "src/main/java/org/oppia/android/domain/feedbackreporting/*.kt",
    "src/main/java/org/oppia/android/domain/onboarding/**/*.kt",
    "src/main/java/org/oppia/android/domain/oppialogger/**/*.kt",
    "src/main/java/org/oppia/android/domain/util/*.kt",
    "src/main/java/org/oppia/android/domain/state/*.kt",
    "src/main/java/org/oppia/android/domain/survey/*.kt",
])

DOMAIN_ASSETS = generate_assets_list_from_text_protos(
    name = "domain_assets",
    classroom_file_names = [
        "test_classroom_id_0",
        "test_classroom_id_1",
        "test_classroom_id_2",
    ],
    classroom_list_file_names = [
        "classrooms",
    ],
    exploration_file_names = [
        "test_exp_id_2",
        "13",
        "test_exp_id_4",
        "test_exp_id_5",
        "umPkwp0L1M0-",
        "MjZzEVOG47_1",
        "2mzzFVDLuAj8",
        "5NWuolNcwH6e",
        "k2bQ7z5XHNbK",
        "tIoSb3HZFN6e",
        "test_single_interactive_state_exp_no_hints_no_solution",
        "test_single_interactive_state_exp_with_hints_and_solution",
        "test_single_interactive_state_exp_with_one_hint_and_solution",
        "test_single_interactive_state_exp_with_one_hint_and_no_solution",
        "test_single_interactive_state_exp_with_only_solution",
        "test_single_interactive_state_exp_with_solution_missing_answer",
        "test_checkpointing_base_exploration",
        "test_checkpointing_exploration_missing_first_state",
        "test_checkpointing_exploration_multiple_compatible_updates",
        "test_checkpointing_exploration_multiple_updates_one_incompatible",
        "test_checkpointing_exploration_new_title",
        "test_checkpointing_exploration_new_version",
        "test_checkpointing_exploration_updated_first_state_compat_rule_spec",
        "test_checkpointing_exploration_updated_first_state_correctness_label",
        "test_checkpointing_exploration_updated_first_state_destination",
        "test_checkpointing_exploration_updated_first_state_feedback_html",
        "test_checkpointing_exploration_updated_first_state_feedback_id",
        "test_checkpointing_exploration_updated_first_state_incompat_rule_spec",
        "test_checkpointing_exploration_updated_first_state_interaction",
        "test_checkpointing_exploration_updated_first_state_new_correct_answer",
        "test_checkpointing_exploration_updated_first_state_updated_hints",
        "test_checkpointing_exploration_updated_second_state_removed",
    ],
    skills_file_names = [
        "skills",
    ],
    story_file_names = [
        "test_story_id_0",
        "test_story_id_2",
        "wANbh4oOClga",
        "wAMdg4oOClga",
        "xBSdg4oOClga",
    ],
    subtopic_file_names = [
        "GJ2rLXRKD5hw_1",
        "GJ2rLXRKD5hw_2",
        "GJ2rLXRKD5hw_3",
        "GJ2rLXRKD5hw_4",
        "omzF4oqgeTXd_1",
        "test_topic_id_0_1",
    ],
    topic_file_names = [
        "test_topic_id_0",
        "test_topic_id_1",
        "test_topic_id_2",
        "GJ2rLXRKD5hw",
        "omzF4oqgeTXd",
    ],
)

kt_android_library(
    name = "domain",
    srcs = glob(
        ["src/main/java/org/oppia/android/domain/**/*.kt"],
        exclude = MIGRATED_PROD_FILES + ["src/main/java/org/oppia/android/domain/testing/**/*.kt"],
    ),
    assets = glob(
        ["src/main/assets/**"],
        exclude = ["src/main/assets/**/*.textproto"],
    ) + DOMAIN_ASSETS,
    assets_dir = "src/main/assets/",
    custom_package = "org.oppia.android.domain",
    manifest = "src/main/AndroidManifest.xml",
    visibility = ["//visibility:public"],
    deps = [
        "//:dagger",
        "//data/src/main/java/org/oppia/android/data/backends/gae/api",
        "//data/src/main/java/org/oppia/android/data/persistence:cache_store",
        "//domain/src/main/java/org/oppia/android/domain/classify:answer_classification_controller",
        "//domain/src/main/java/org/oppia/android/domain/classify:classification_result",
        "//domain/src/main/java/org/oppia/android/domain/oppialogger:oppia_logger",
        "//domain/src/main/java/org/oppia/android/domain/oppialogger:startup_listener",
        "//domain/src/main/java/org/oppia/android/domain/oppialogger/analytics:analytics_startup_listener",
        "//domain/src/main/java/org/oppia/android/domain/oppialogger/exceptions:controller",
        "//domain/src/main/java/org/oppia/android/domain/oppialogger/logscheduler:metric_log_scheduling_worker_factory",
        "//domain/src/main/java/org/oppia/android/domain/oppialogger/loguploader:worker_factory",
        "//domain/src/main/java/org/oppia/android/domain/profile:profile_management_controller",
        "//domain/src/main/java/org/oppia/android/domain/state:state_deck",
        "//domain/src/main/java/org/oppia/android/domain/state:state_graph",
        "//domain/src/main/java/org/oppia/android/domain/state:state_list",
        "//domain/src/main/java/org/oppia/android/domain/translation:translation_controller",
        "//domain/src/main/java/org/oppia/android/domain/util:asset",
        "//domain/src/main/java/org/oppia/android/domain/util:extensions",
        "//domain/src/main/java/org/oppia/android/domain/util:retriever",
        "//model/src/main/proto:exploration_checkpoint_java_proto_lite",
        "//model/src/main/proto:onboarding_java_proto_lite",
        "//model/src/main/proto:platform_parameter_java_proto_lite",
        "//model/src/main/proto:question_java_proto_lite",
        "//model/src/main/proto:topic_java_proto_lite",
        "//third_party:androidx_work_work-runtime-ktx",
        "//third_party:org_jetbrains_kotlinx_kotlinx-coroutines-guava",
        "//utility/src/main/java/org/oppia/android/util/data:data_providers",
        "//utility/src/main/java/org/oppia/android/util/extensions:bundle_extensions",
        "//utility/src/main/java/org/oppia/android/util/extensions:context_extensions",
        "//utility/src/main/java/org/oppia/android/util/extensions:string_extensions",
        "//utility/src/main/java/org/oppia/android/util/logging:event_logger",
        "//utility/src/main/java/org/oppia/android/util/logging:log_uploader",
        "//utility/src/main/java/org/oppia/android/util/math:extensions",
        "//utility/src/main/java/org/oppia/android/util/math:math_expression_parser",
        "//utility/src/main/java/org/oppia/android/util/networking:network_connection_util",
        "//utility/src/main/java/org/oppia/android/util/parser/html:exploration_html_parser_entity_type",
        "//utility/src/main/java/org/oppia/android/util/parser/image:image_parsing_annonations",
        "//utility/src/main/java/org/oppia/android/util/profile:directory_management_util",
    ],
)

# TODO(#2143): Move InteractionObjectTestBuilder to a testing package outside the test folder.
kt_android_library(
    name = "interaction_object_test_builder",
    testonly = True,
    srcs = [
        "src/test/java/org/oppia/android/domain/classify/InteractionObjectTestBuilder.kt",
    ],
    deps = [
        "//model/src/main/proto:question_java_proto_lite",
    ],
)

# keep sorted
TEST_DEPS = [
    ":domain",
    ":interaction_object_test_builder",
    "//:dagger",
    "//app:crashlytics",
    "//app:crashlytics_deps",
    "//app:firestore_deps",
    "//app/src/main/java/org/oppia/android/app/application/testing:testing_build_flavor_module",
    "//config/src/java/org/oppia/android/config:all_languages_config",
    "//data/src/main/java/org/oppia/android/data/backends/gae:network_config_prod_module",
    "//data/src/main/java/org/oppia/android/data/backends/gae/model",
    "//data/src/main/java/org/oppia/android/data/persistence:cache_store",
    "//domain/src/main/java/org/oppia/android/domain/audio:audio_player_controller",
    "//domain/src/main/java/org/oppia/android/domain/audio:cellular_audio_dialog_controller",
    "//domain/src/main/java/org/oppia/android/domain/classify:interactions_module",
    "//domain/src/main/java/org/oppia/android/domain/classify/rules/algebraicexpressioninput:algebraic_expression_input_rule_module",
    "//domain/src/main/java/org/oppia/android/domain/classify/rules/continueinteraction:continue_module",
    "//domain/src/main/java/org/oppia/android/domain/classify/rules/dragAndDropSortInput:drag_and_drop_sort_input_module",
    "//domain/src/main/java/org/oppia/android/domain/classify/rules/fractioninput:fraction_input_module",
    "//domain/src/main/java/org/oppia/android/domain/classify/rules/imageClickInput:image_click_input_module",
    "//domain/src/main/java/org/oppia/android/domain/classify/rules/itemselectioninput:item_selection_input_module",
    "//domain/src/main/java/org/oppia/android/domain/classify/rules/mathequationinput:math_equation_input_rule_module",
    "//domain/src/main/java/org/oppia/android/domain/classify/rules/multiplechoiceinput:multiple_choice_input_module",
    "//domain/src/main/java/org/oppia/android/domain/classify/rules/numberwithunits:number_with_units_rule_module",
    "//domain/src/main/java/org/oppia/android/domain/classify/rules/numericexpressioninput:numeric_expression_input_rule_module",
    "//domain/src/main/java/org/oppia/android/domain/classify/rules/numericinput:numeric_input_rule_module",
    "//domain/src/main/java/org/oppia/android/domain/classify/rules/ratioinput:ratio_input_module",
    "//domain/src/main/java/org/oppia/android/domain/classify/rules/textinput:text_input_rule_module",
    "//domain/src/main/java/org/oppia/android/domain/feedbackreporting:prod_module",
    "//domain/src/main/java/org/oppia/android/domain/feedbackreporting:report_schema_version",
    "//domain/src/main/java/org/oppia/android/domain/onboarding:deprecation_controller",
    "//domain/src/main/java/org/oppia/android/domain/onboarding:retriever_prod_module",
    "//domain/src/main/java/org/oppia/android/domain/onboarding:state_controller",
    "//domain/src/main/java/org/oppia/android/domain/oppialogger:prod_module",
    "//domain/src/main/java/org/oppia/android/domain/oppialogger/analytics:cpu_module",
    "//domain/src/main/java/org/oppia/android/domain/oppialogger/analytics:prod_module",
    "//domain/src/main/java/org/oppia/android/domain/oppialogger/analytics/testing:fake_log_scheduler",
    "//domain/src/main/java/org/oppia/android/domain/oppialogger/exceptions:logger_module",
    "//domain/src/main/java/org/oppia/android/domain/oppialogger/exceptions:startup_listener",
    "//domain/src/main/java/org/oppia/android/domain/oppialogger/logscheduler:metric_log_scheduler_module",
    "//domain/src/main/java/org/oppia/android/domain/oppialogger/loguploader:worker_factory",
    "//domain/src/main/java/org/oppia/android/domain/oppialogger/loguploader:worker_module",
    "//domain/src/main/java/org/oppia/android/domain/spotlight:spotlight_state_controller",
    "//domain/src/main/java/org/oppia/android/domain/testing/oppialogger/loguploader:fake_log_uploader",
    "//model/src/main/proto:spotlight_java_proto_lite",
    "//testing",
    "//testing/src/main/java/org/oppia/android/testing/data:async_result_subject",
    "//testing/src/main/java/org/oppia/android/testing/data:data_provider_test_monitor",
    "//testing/src/main/java/org/oppia/android/testing/logging:event_log_subject",
    "//testing/src/main/java/org/oppia/android/testing/logging:sync_status_test_module",
    "//testing/src/main/java/org/oppia/android/testing/logging:test_sync_status_manager",
    "//testing/src/main/java/org/oppia/android/testing/network:network",
    "//testing/src/main/java/org/oppia/android/testing/network:test_module",
    "//testing/src/main/java/org/oppia/android/testing/platformparameter:test_module",
    "//testing/src/main/java/org/oppia/android/testing/robolectric:test_module",
    "//testing/src/main/java/org/oppia/android/testing/threading:test_module",
    "//testing/src/main/java/org/oppia/android/testing/time:test_module",
    "//third_party:androidx_arch_core_core-testing",
    "//third_party:androidx_test_ext_junit",
    "//third_party:androidx_work_work-testing",
    "//third_party:com_google_truth_extensions_truth-liteproto-extension",
    "//third_party:com_google_truth_truth",
    "//third_party:org_jetbrains_kotlin_kotlin-reflect",
    "//third_party:org_jetbrains_kotlin_kotlin-test-junit",
    "//third_party:robolectric_android-all",
    "//utility/src/main/java/org/oppia/android/util/caching:asset_prod_module",
    "//utility/src/main/java/org/oppia/android/util/caching/testing:caching_test_module",
    "//utility/src/main/java/org/oppia/android/util/caching/testing:fake_asset_repository",
    "//utility/src/main/java/org/oppia/android/util/extensions:context_extensions",
    "//utility/src/main/java/org/oppia/android/util/locale:prod_module",
    "//utility/src/main/java/org/oppia/android/util/logging:prod_module",
    "//utility/src/main/java/org/oppia/android/util/logging/firebase:prod_module",
    "//utility/src/main/java/org/oppia/android/util/logging/performancemetrics:performance_metrics_assessor_module",
    "//utility/src/main/java/org/oppia/android/util/logging/performancemetrics:performance_metrics_configurations_module",
    "//utility/src/main/java/org/oppia/android/util/networking:debug_module",
    "//utility/src/main/java/org/oppia/android/util/networking:debug_util_module",
    "//utility/src/main/java/org/oppia/android/util/system:prod_module",
]

# Qualified file paths for test classes that have been migrated over to their own packages &
# shouldn't be defined as module-level tests.
# keep sorted
MIGRATED_TESTS = [
]

[domain_test(
    name = test_file_path,
    filtered_tests = MIGRATED_TESTS,
    deps = TEST_DEPS,
) for test_file_path in glob(["src/test/java/org/oppia/android/domain/**/*Test.kt"])]
