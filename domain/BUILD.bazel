# TODO(#1532): Rename file to 'BUILD' post-Gradle.
"""
This library contains the frontend controller and business service logic for the application.
"""

load("@dagger//:workspace_defs.bzl", "dagger_rules")
load("@io_bazel_rules_kotlin//kotlin:kotlin.bzl", "kt_android_library")
load("//domain:domain_test.bzl", "domain_test")

kt_android_library(
    name = "domain",
    srcs = glob(
        ["src/main/java/org/oppia/android/domain/**/*.kt"],
        exclude = ["src/main/java/org/oppia/android/domain/testing/**/*.kt"],
    ),
    assets = glob(["src/main/assets/**"]),
    assets_dir = "src/main/assets/",
    custom_package = "org.oppia.android.domain",
    manifest = "src/main/AndroidManifest.xml",
    visibility = ["//visibility:public"],
    deps = [
        ":dagger",
        "//data:persistent_cache_store",
        "//model:onboarding_java_proto_lite",
        "//third_party:androidx_work_work-runtime-ktx",
    ],
)

kt_android_library(
    name = "testing",
    testonly = True,
    srcs = glob(["src/main/java/org/oppia/android/domain/testing/**/*.kt"]),
    deps = [
        ":domain",
        "//third_party:androidx_work_work-runtime-ktx",
    ],
)

# TODO(#2143): Move InteractionObjectTestBuilder to a testing package outside the test folder.
kt_android_library(
    name = "interaction_object_test_builder",
    testonly = True,
    srcs = [
        "src/test/java/org/oppia/android/domain/classify/InteractionObjectTestBuilder.kt",
    ],
    deps = [
        "//model",
    ],
)

# keep sorted
TEST_DEPS = [
    ":dagger",
    ":domain",
    ":interaction_object_test_builder",
    ":testing",
    "//data:persistent_cache_store",
    "//model",
    "//testing",
    "//third_party:androidx_arch_core_core-testing",
    "//third_party:androidx_test_ext_junit",
    "//third_party:androidx_work_work-testing",
    "//third_party:com_google_truth_truth",
    "//third_party:org_jetbrains_kotlin_kotlin-reflect",
    "//third_party:org_jetbrains_kotlin_kotlin-test-junit",
    "//third_party:robolectric_android-all",
]

# Qualified file paths for test classes that have been migrated over to their own packages &
# shouldn't be defined as module-level tests.
# keep sorted
MIGRATED_TESTS = [
]

[domain_test(
    name = test_file_path,
    filtered_tests = MIGRATED_TESTS,
    deps = TEST_DEPS,
) for test_file_path in glob(["src/test/java/org/oppia/android/domain/**/*Test.kt"])]

dagger_rules()
