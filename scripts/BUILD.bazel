"""
Kotlin-based scripts to help developers or perform continuous integration tasks.
"""

load("@io_bazel_rules_kotlin//kotlin:kotlin.bzl", "kt_jvm_binary", "kt_jvm_library")
load("//scripts:script_assets.bzl", "generate_maven_assets_list_from_text_protos", "generate_regex_assets_list_from_text_protos")

# Visibility for libraries that should be accessible to script tests.
package_group(
    name = "oppia_script_test_visibility",
    packages = [
        "//scripts/src/javatests/...",
    ],
)

# Visibility for libraries that have binaries defined at this package & tests.
package_group(
    name = "oppia_script_binary_visibility",
    includes = [
        ":oppia_script_test_visibility",
    ],
    packages = [
        "//scripts",
    ],
)

# Visibility for libraries that should be accessible to other script packages & tests.
package_group(
    name = "oppia_script_library_visibility",
    includes = [
        ":oppia_script_test_visibility",
    ],
    packages = [
        "//scripts/src/java/...",
    ],
)

kt_jvm_binary(
    name = "compute_affected_tests",
    testonly = True,
    main_class = "org.oppia.android.scripts.ci.ComputeAffectedTestsKt",
    runtime_deps = [
        "//scripts/src/java/org/oppia/android/scripts/ci:compute_affected_tests_lib",
    ],
)

# TODO(#3428): Refactor textproto assets to subpackage level.
REGEX_PATTERN_CHECK_ASSETS = generate_regex_assets_list_from_text_protos(
    name = "regex_asset_files",
    file_content_validation_file_names = [
        "file_content_validation_checks",
    ],
    filepath_pattern_validation_file_names = [
        "filename_pattern_validation_checks",
    ],
)

MAVEN_ASSETS = generate_maven_assets_list_from_text_protos(
    name = "maven_assets",
    maven_dependencies_file_name = [
        "maven_dependencies",
    ],
)

kt_jvm_library(
    name = "regex_check_assets",
    data = REGEX_PATTERN_CHECK_ASSETS,
    visibility = [":oppia_script_test_visibility"],
)

kt_jvm_binary(
    name = "regex_pattern_validation_check",
    testonly = True,
    data = REGEX_PATTERN_CHECK_ASSETS,
    main_class = "org.oppia.android.scripts.regex.RegexPatternValidationCheckKt",
    runtime_deps = [
        "//scripts/src/java/org/oppia/android/scripts/regex:regex_pattern_validation_check_lib",
    ],
)

kt_jvm_binary(
    name = "proto_check",
    testonly = True,
    main_class = "org.oppia.android.scripts.regex.ProtoClassKt",
    runtime_deps = ["//scripts/src/java/org/oppia/android/scripts/regex:proto_class_lib"],
)

kt_jvm_library(
    name = "maven_assets_lib",
    data = MAVEN_ASSETS,
    visibility = [":oppia_script_test_visibility"],
)

kt_jvm_binary(
    name = "generate_maven_dependencies_list",
    data = MAVEN_ASSETS,
    main_class = "org.oppia.android.scripts.maven.GenerateMavenDependenciesListKt",
    runtime_deps = ["//scripts/src/java/org/oppia/android/scripts/maven:generate_maven_dependencies_list_lib"],
)
